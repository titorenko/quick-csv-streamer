plugins {
   id 'me.champeau.gradle.jmh' version '0.2.0'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'me.champeau.gradle.jmh'
apply plugin: "eclipse"

group = 'uk.elementarysoftware'
version = '0.1.2-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_1_8


repositories {
    mavenCentral()
}

dependencies {	
    testCompile group: 'commons-io', name: 'commons-io', version: '2.4'
    testCompile group: 'junit', name: 'junit', version: '4.11'
	    
    jmh "commons-io:commons-io:2.4"    
    jmh "net.sf.opencsv:opencsv:2.3"
}

jmh {
    include = '.*BenchmarkParserAndMapperInMemory.*' 
    jmhVersion = '1.8'
    jvmArgsAppend = '-Xmx1g -XX:+AggressiveOpts'     
}

eclipse {
    classpath {
        plusConfigurations += [ configurations.jmh ]
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (hasProperty('ossrhUsername')) {
	
	signing {
	    sign configurations.archives
	}

	uploadArchives {
	  repositories {
	    mavenDeployer {
	      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

	      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
	        authentication(userName: ossrhUsername, password: ossrhPassword)
	      }

	      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
	        authentication(userName: ossrhUsername, password: ossrhPassword)
	      }

	      pom.project {
	        name 'Quick CSV Streamer'
	        packaging 'jar'
	        description 'Quick CSV Parser with Java 8 Streams API'
	        url 'https://github.com/titorenko/quick-csv-streamer'

	        scm {
	          connection 'scm:git:git://github.com/titorenko/quick-csv-streamer.git'
	          developerConnection 'scm:git:git@github.com:titorenko/quick-csv-streamer.git'
	          url 'https://github.com/titorenko/quick-csv-streamer'
	        }

	        licenses {
	          license {
	            name 'GNU General Public License, version 2'
	            url 'http://www.gnu.org/licenses/gpl-2.0.html'
	          }
	        }

	        developers {
	          developer {
	            id 'elementarysoftware'
	            name 'Elementary Software Ltd.'
	            email 'elementarysoftware@gmail.com'
	          }
	        }
	      }
	    }
	  }
	}
}